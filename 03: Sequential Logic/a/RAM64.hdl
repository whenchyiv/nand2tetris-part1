// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/RAM64.hdl

/**
 * Memory of 64 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    // Select RAM Chip
    DMux8Way(in=load, sel=address[3..5], a=ram0, b=ram1, c=ram2, d=ram3, e=ram4, f=ram5, g=ram6, h=ram7);
    
    // RAMs
    RAM8(in=in, load=ram0, address=address[0..2], out=ramOut0);
    RAM8(in=in, load=ram1, address=address[0..2], out=ramOut1);
    RAM8(in=in, load=ram2, address=address[0..2], out=ramOut2);
    RAM8(in=in, load=ram3, address=address[0..2], out=ramOut3);
    RAM8(in=in, load=ram4, address=address[0..2], out=ramOut4);
    RAM8(in=in, load=ram5, address=address[0..2], out=ramOut5);
    RAM8(in=in, load=ram6, address=address[0..2], out=ramOut6);
    RAM8(in=in, load=ram7, address=address[0..2], out=ramOut7);

    Mux8Way16(a=ramOut0, b=ramOut1, c=ramOut2, d=ramOut3, e=ramOut4, f=ramOut5, g=ramOut6, h=ramOut7, sel=address[3..5], out=out);
}